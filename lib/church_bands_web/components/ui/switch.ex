defmodule ChurchBandsWeb.Components.UI.Switch do
  @moduledoc """
  Implementation of a switch/toggle component.

  A switch is a control that allows users to toggle between checked and not checked states.

  ## Component events

  * `:on-checked-changed` - Maps the `checked-changed` event. Fired when the switch is toggled.

  ## Server commands

  * `"toggle"` - Toggles the switch between checked and unchecked.
  """
  use ChurchBandsWeb.Components.UI, :component

  @doc """
  Renders a switch component.

  ## Props
    * `:id` - The id to be applied to the input element
    * `:name` - The name to be applied to the input element
    * `:class` - Additional CSS classes
    * `:value` - The current value of the switch
    * `:default-value` - The default value of the switch
    * `:field` - Phoenix form field
    * `:disabled` - Whether the switch is disabled
    * `:on-checked-changed` - Handler for value change event

  ## Component events

  * `:on-checked-changed` - Fired when the switch is toggled.

  ## Server commands

  * `"toggle"` - Toggles the switch between checked and unchecked.
  """
  attr :id, :string, default: nil
  attr :name, :string, default: nil
  attr :class, :string, default: nil
  attr :value, :any, default: nil
  attr :"default-value", :any, values: [true, false, "true", "false"], default: false
  attr :checked, :boolean
  attr :disabled, :boolean, default: false
  attr :"on-checked-changed", :any, default: nil, doc: "Handler for value change event"

  attr :field, Phoenix.HTML.FormField,
    doc: "a form field struct retrieved from the form, for example: @form[:active]"

  attr :rest, :global

  def switch(assigns) do
    assigns = prepare_assign(assigns)

    # Normalize value for boolean
    assigns = assign_new(assigns, :checked, fn -> normalize_boolean(assigns.value) end)

    # Collect event mappings
    event_map = event_mappings(assigns)

    assigns =
      assigns
      |> assign(:event_map, json(event_map))
      |> assign(:initial_state, if(assigns.checked, do: "checked", else: "unchecked"))
      |> assign(
        :options,
        json(%{
          disabled: assigns.disabled
        })
      )

    ~H"""
    <div
      id={@id}
      data-component="switch"
      data-state={@initial_state}
      data-options={@options}
      data-event-mappings={@event_map}
      data-part="root"
      phx-hook="SaladUI"
      data-disabled={@disabled}
      role="switch"
      aria-checked={to_string(@checked)}
      class={
        classes([
          "switch-root inline-flex h-6 w-11 shrink-0 cursor-pointer items-center rounded-full border-2 border-transparent transition-colors focus-visible:outline-hidden focus-visible:ring-ring/50 focus-visible:ring-[3px] data-[disabled]:cursor-not-allowed data-[disabled]:opacity-50 data-[state=checked]:bg-primary data-[state=unchecked]:bg-input",
          @class
        ])
      }
      tabindex={if @disabled, do: "-1", else: "0"}
      {@rest}
    >
      <input :if={!@disabled} type="hidden" name={@name} value="false" />
      <input
        type="checkbox"
        id={"#{@id}-input"}
        name={@name}
        value="true"
        class="sr-only"
        checked={@checked}
        disabled={@disabled}
        aria-checked={to_string(@checked)}
        data-part="input"
      />
      <span
        data-part="thumb"
        data-state={@initial_state}
        class="pointer-events-none block h-5 w-5 rounded-full bg-background shadow-lg ring-0 transition-transform data-[state=checked]:translate-x-5 data-[state=unchecked]:translate-x-0"
      />
    </div>
    """
  end

  defp normalize_boolean(value) do
    case value do
      "true" -> true
      "false" -> false
      true -> true
      false -> false
      _ -> false
    end
  end
end
